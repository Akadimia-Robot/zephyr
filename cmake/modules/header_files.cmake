# SPDX-License-Identifier: Apache-2.0
#
# Copyright (c) 2023, Google LLC

include_guard(GLOBAL)

# Generate headers are used by the base system.
# The logic for generating devicetree_generated.h is kept separately
# in dts.cmake.
#
# Outcome:
# ${BINARY_DIR_INCLUDE_GENERATED}/sensor_channels.h exists
#
# Required variables:
# BINARY_DIR_INCLUDE_GENERATED: where to put generated include files
#
# Optional variables:
# None
#
# Optional environment variables:
# None

set(SENSOR_CHANNELS_INPUT  ${ZEPHYR_BASE}/include/zephyr/dt-bindings/sensor/sensor.h)
set(SENSOR_CHANNELS_LINE_REGEX  "^#define[ \t\r\n]*SENSOR_CHAN_.*$")
set(SENSOR_CHANNELS_ENUM_REGEX  "^#define[ \t\r\n]+(SENSOR_CHAN_[^ \t\r\n]+)[ \t\r\n]+([^ \t\r\n]+).*$")
set(SENSOR_CHANNELS        ${BINARY_DIR_INCLUDE_GENERATED}/sensor_channels.h)

# Generate an enum from an array of strings containing macro definitions
# TODO document this
function(zephyr_gen_enum_from_macros name list out)
# TODO Add header guards and "generated by" text
  file(WRITE ${out} "enum " ${name} "{\n")
  foreach(line ${list})
    string(REGEX REPLACE ${SENSOR_CHANNELS_ENUM_REGEX} "\t\\1 = \\2,\n" symbol ${line})
    file(APPEND ${out} ${symbol})
  endforeach()
  file(APPEND ${out} "};")
endfunction()

file(STRINGS ${SENSOR_CHANNELS_INPUT} channels REGEX ${SENSOR_CHANNELS_LINE_REGEX})
zephyr_gen_enum_from_macros("sensor_channel" "${channels}" ${SENSOR_CHANNELS})
